plugins {
    alias libs.plugins.android.library
    // Error when using "alias"
    id 'org.jetbrains.kotlin.multiplatform'
    alias libs.plugins.sqldelight
}

kotlin {
    androidTarget {
        compilations.configureEach {
            kotlinOptions {
                jvmTarget = '21'
            }
        }
    }

    [iosX64(), iosArm64(), iosSimulatorArm64()].each {
        it.binaries.framework {
            baseName = 'shared'
            isStatic = true
        }
    }

    sourceSets {
        commonMain.dependencies {
            implementation libs.runtime
            implementation libs.primitive.adapters
            implementation libs.koin.core
            implementation libs.kotlinx.datetime
            implementation libs.coroutines.extensions
        }
        androidMain.dependencies {
            implementation libs.android.driver
        }
        iosMain.dependencies {
            implementation libs.native.driver
        }
    }
}

android {
    namespace = 'com.mquniversity.tcct.shared'
    compileSdk 34
    defaultConfig {
        minSdk 26
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
}

sqldelight {
    databases {
        AppDatabase {
            packageName = 'com.mquniversity.tcct.shared.cache'
        }
    }
}
